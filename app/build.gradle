buildscript {
  repositories {
    maven { url 'http://download.crashlytics.com/maven' }
  }

  dependencies {
    classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.16.0'
  }
}
apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

repositories {
  maven { url 'http://download.crashlytics.com/maven' }
}


android {
  compileSdkVersion 22
  buildToolsVersion "22.0.1"

  def versionPropsFile = file('version.properties')

  if (versionPropsFile.canRead()) {
    def Properties versionProps = new Properties()

    versionProps.load(new FileInputStream(versionPropsFile))
    def value = 0
    def runTasks = gradle.startParameter.taskNames
    if ('assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks
            || 'generateReleaseBuildConfig' in runTasks) {
      value = 1;
    }

    def versionMajor = 1
    def versionMinor = 3
    def versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
    def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
    def version_Code = versionProps['VERSION_CODE'].toInteger() + value

    versionProps['VERSION_PATCH'] = versionPatch.toString()
    versionProps['VERSION_BUILD'] = versionBuild.toString()
    versionProps['VERSION_CODE'] = version_Code.toString()

    versionProps.store(versionPropsFile.newWriter(), null)

    defaultConfig {
      applicationId "com.novachevskyi.expenseslite"
      minSdkVersion 16
      targetSdkVersion 22
      versionCode version_Code
      versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild})"
    }
  } else {
    throw new GradleException("Could not read version.properties!")
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  packagingOptions {
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'LICENSE.txt'
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.fasterxml.jackson.core:jackson-databind:2.4.5'
  compile 'com.fasterxml.jackson.core:jackson-core:2.4.5'
  compile 'com.fasterxml.jackson.core:jackson-annotations:2.4.5'
  compile 'com.squareup.retrofit:retrofit:1.6.1'
  compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
  compile 'com.squareup.okhttp:okhttp:2.0.0'
  compile 'com.android.support:support-v4:22.0.0'
  compile 'com.android.support:appcompat-v7:22.0.0'
  compile 'com.android.support:recyclerview-v7:21.0.3'
  compile 'com.android.support:cardview-v7:21.0.3'
  compile 'com.getbase:floatingactionbutton:1.9.0'
  compile 'com.jakewharton:butterknife:6.0.0'
  compile 'net.danlew:android.joda:2.7.1'
  compile 'com.github.blackfizz:eazegraph:1.2.2@aar'
  compile 'com.nineoldandroids:library:2.4.0'
  compile 'com.google.android.gms:play-services:6.1.71'
  compile 'com.crashlytics.android:crashlytics:1.1.13'
  compile files('libs/FlurryAnalytics-5.4.0.jar')
  compile 'com.novachevskyi:material-calendar-datepicker:1.0.0'
  compile project(':rateapp')
}